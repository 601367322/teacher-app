apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'


def Test_Setting_close = 'close'
def Test_Setting_open = 'open'

//读取签名配置文件
Properties props = new Properties()
props.load(new FileInputStream(file("../buildsystem/signing.properties")))

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.prance.teacher"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        //只有中文
        resConfigs "zh"
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }
        // 测试开关
        manifestPlaceholders.put("TEST_SETTING", "$Test_Setting_close")
        //Bugly
        manifestPlaceholders.put("BUGLY_APPID", "c3f5c89f7c")
    }

    signingConfigs {

        release {
            keyAlias props['KEY_ALIAS']
            keyPassword props['KEY_PASSWORD']
            storeFile file(props['KEYSTORE_FILE'])
            storePassword props['KEYSTORE_PASSWORD']

            v1SigningEnabled true
            v2SigningEnabled true
        }
    }


    buildTypes {

        debug {
            minifyEnabled false

            signingConfig signingConfigs.release
            zipAlignEnabled true
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            ndk {
                abiFilters "armeabi-v7a"
            }

            zipAlignEnabled true
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions("normal")

    //渠道列表
    productFlavors {

        dev {
            manifestPlaceholders.put("TEST_SETTING", "$Test_Setting_open")
            manifestPlaceholders.put("APP_CHANNEL", "dev")
            dimension "normal"
        }

        prod {
            dimension "normal"
            manifestPlaceholders.put("TEST_SETTING", "$Test_Setting_close")
            manifestPlaceholders.put("APP_CHANNEL", "prod")
        }
    }

    applicationVariants.all { variant ->

        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName = "app-release.apk"
                outputFileName = new File(fileName)
            }
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    def dependencies = rootProject.ext.dependencies

    kapt dependencies.permissionsdispatcherProcessor

    implementation dependencies.constraintLayout
    implementation dependencies.kotlin
    implementation dependencies.v4
    implementation dependencies.recyclerview
    implementation dependencies.openGlCanvas
    implementation dependencies.okhttp3
    implementation dependencies.cardview
    implementation dependencies.rxjava
    implementation dependencies.rxandroid
    implementation dependencies.gson
    implementation dependencies.utilcode
    implementation dependencies.eventbus
    implementation dependencies.pageindicatorview
    implementation dependencies.glideTransformations
    implementation dependencies.baseadapter
    implementation dependencies.downloader
    implementation dependencies.downloaderSqlite
    implementation dependencies.downloaderOkHttp
    implementation (dependencies.retrofit2) {
        exclude group: 'com.squareup.okhttp3'
    }
    implementation(dependencies.permissionsdispatcher) {
        exclude group: 'com.android.support'
    }
    implementation(dependencies.glide) {
        exclude group: "com.android.support"
    }

    implementation project(":lib.base")
    implementation project(":lib.teacher.base")
    implementation project(":lib.test.setting")
    implementation project(":lib.interfaces")
    implementation project(":lib.server.vo")
    implementation project(":lib.bugly")
    implementation project(":lib.common")
    implementation project(":lib.database")
    implementation project(":lib.qrcode")
    implementation project(":lib.socket")
    implementation project(":lib.sharedpreferences")
    implementation project(":lib.dimens")
    implementation project(":lib.spark")
}
repositories {
    mavenCentral()
}
